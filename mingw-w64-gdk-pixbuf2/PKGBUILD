pkgname=mingw-w64-gdk-pixbuf2
pkgver=2.31.6
pkgrel=1
pkgdesc='An image loading library (mingw-w64)'
arch=(any)
url='http://www.gtk.org'
license=('LGPL')
makedepends=(
  'mingw-w64-gcc'
  'mingw-w64-pkg-config'
  'mingw-w64-configure')
depends=(
  'mingw-w64-crt'
  'mingw-w64-glib2>=2.37.6'
  'mingw-w64-jasper'
  'mingw-w64-libpng'
  'mingw-w64-libjpeg-turbo'
  'mingw-w64-libtiff')
options=(!strip !buildflags staticlibs)
source=(
  "https://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
  "0001-Use-a-regex-to-properly-export-the-symbols.patch"
  "0002-gdk-pixbuf-introspection.patch")
sha256sums=('111203d6f295d69d36e7190204248331cd37285130ac483becdedcd3f0135d18'
            'e8d278e30c44e973e14e3c61e8ab195621d6a9a402e0da557db4616955ca4543'
            'c22ce6c4de87c9f6e8b18452a728e4677fac18d2b51d6e536db27160c368f0c6')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"
  patch -p1 -i ${srcdir}/0001-Use-a-regex-to-properly-export-the-symbols.patch
  patch -p0 -i ${srcdir}/0002-gdk-pixbuf-introspection.patch
}

build() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"
  
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}"
    cd "build-${_arch}"
    ${_arch}-configure \
        --enable-static \
        --enable-shared \
        --with-included-loaders \
        --with-libjasper
    make
    cd ..
  done
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"
  for _arch in ${_architectures}; do
    cd "build-${_arch}"
    make DESTDIR="$pkgdir" install
    #find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm
    #find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    #find "$pkgdir/usr/${_arch}" -name '*.a' | xargs -rtl1 ${_arch}-strip -g
    #rm -r "$pkgdir/usr/${_arch}/share/man"
    cd ..
  done
}
