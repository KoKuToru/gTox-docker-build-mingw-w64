pkgname=mingw-w64-gstreamermm
pkgver=1.4.3
pkgrel=1
pkgdesc="C++ interface for GStreamer"
arch=(any)
license=('LGPL')
url="http://gstreamer.freedesktop.org/bindings/cplusplus.html"
 
options=('!strip' '!buildflags' 'staticlibs')
depends=(mingw-w64-{glib2,libxml2})
makedepends=(mingw-w64-configure bison flex python)
 
source=(http://ftp.gnome.org/pub/GNOME/sources/gstreamermm/1.4/gstreamermm-$pkgver.tar.xz)
 
sha256sums=('f1c11ee1cf7537d77de7f8d486e09c5140cc4bb78882849718cd88959a55462e')
 
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"
 
 
build() {
  cd ${srcdir}/gstreamermm-${pkgver}
  
  export CFLAGS="-O2 -pipe -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="$CFLAGS"
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure \
      --prefix=/usr/${_arch} \
      --with-package-origin="http://www.archlinux.org/" \
      --disable-unittests 
    make
    popd
  done
}
 
 
package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/gstreamermm-${pkgver}/build-${_arch}
    make DESTDIR="${pkgdir}" install
    
    find "$pkgdir" -name '*.dll' -exec ${_arch}-strip --strip-unneeded {} \;
    find "$pkgdir" -name '*.dll' -o -name '*.a' -exec ${_arch}-strip -g {} \;
  done
}
